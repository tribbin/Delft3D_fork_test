cmake_minimum_required(VERSION 3.30)

project(CMake)
enable_testing()

# enable project files grouping by folders
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

message(STATUS "Configuring")
if (UNIX)
    include(linux_scripts.cmake)
    add_definitions(-DCOMMIT_VERSION=${COMMIT_VERSION})
endif(UNIX)

include(functions.cmake)

# Set checkout root
set(checkout_src_root ${CMAKE_CURRENT_SOURCE_DIR}/..) # This points to the location of OSS/src

# main repository directory
cmake_path(GET CMAKE_CURRENT_SOURCE_DIR PARENT_PATH PARENT_DIR)
cmake_path(GET PARENT_DIR PARENT_PATH dir)


# Set unit and integration test root directory
set(unit_tests_dir ${dir}/test/unit_test)
set(integration_tests_dir ${dir}/test/integration_test)

include(modules.cmake)

# Check CMake optional arguments
set(CONFIGURATION_TYPE "ALL" CACHE STRING "Sets the configuration to be generated by this script.")
message(STATUS "build nr: ${COMMIT_VERSION}")
# Set this to true to group all predefined projects
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# Set compiler flags
enable_language(Fortran C CXX)

if(${CMAKE_Fortran_COMPILER_ID} MATCHES "GNU")
    include(compiler_options/gnu.cmake)
elseif(${CMAKE_Fortran_COMPILER_ID} MATCHES "Intel")
    get_intel_version()
    include(compiler_options/intel.cmake)
else()
    message(FATAL_ERROR "Compiler \"${CMAKE_Fortran_COMPILER_ID}\" does not match known compilers.")
endif()
include(compiler_options/compiler_warning_targets.cmake)

# Set available build configurations
set (CMAKE_CONFIGURATION_TYPES "Debug;RelWithDebInfo;Release" CACHE STRING "" FORCE)

# Set Fortran module files location (needed for dependency bubbling on Linux)
set(CMAKE_Fortran_MODULE_DIRECTORY ${CMAKE_BINARY_DIR}/fortran_module_dir)

# MPI
set(OSS_MPI "IntelMPI" CACHE STRING "MPI library is IntelMPI")

if (WIN32)
    message(CHECK_START "Setting paths for windows")
    list(APPEND CMAKE_MESSAGE_INDENT "  ")

    # Set pthreads library
    set(pthreads_path ${checkout_src_root}/third_party_open/pthreads/include/x64)
    message(STATUS "pthreads set to ${pthreads_path}")

    # Set mpi_include_path
    if ("${OSS_MPI}" STREQUAL "IntelMPI")
        message(STATUS "Intel MPI")
        list(APPEND CMAKE_MESSAGE_INDENT "  ")

        if(intel_version GREATER_EQUAL 24)
            message(STATUS "Using Intel 2024 folder structure.")
            set(mpi_include_path $ENV{ONEAPI_ROOT}/mpi/latest/include)
            set(mpi_module_path ${mpi_include_path}/mpi)
            set(mpi_library_path $<IF:$<CONFIG:Debug>,$ENV{ONEAPI_ROOT}/mpi/latest/lib/mpi/debug,$ENV{ONEAPI_ROOT}/mpi/latest/lib>)
        else()
            message(DEPRECATION "Using Intel 2023 folder structure (I_MPI_ONEAPI_ROOT is set to: $ENV{I_MPI_ONEAPI_ROOT})")
            set(mpi_include_path $ENV{I_MPI_ONEAPI_ROOT}/include)
            set(mpi_module_path ${mpi_include_path})
            set(mpi_library_path $ENV{I_MPI_ONEAPI_ROOT}/lib/$<IF:$<CONFIG:Debug>,debug,release>;$ENV{I_MPI_ONEAPI_ROOT}/lib)
        endif()
        set(mpi_fortran_library "impi.lib")
        set(mpi_c_library "impi.lib;impicxx.lib")
    else()
        message(WARNING "Did not recognize MPI library \"${OSS_MPI}\"")
    endif()

    message(STATUS "mpi include path \"${mpi_include_path}\"")
    message(STATUS "mpi module path \"${mpi_module_path}\"")
    message(STATUS "mpi library path \"${mpi_library_path}\"")
    message(STATUS "mpi fortran library \"${mpi_fortran_library}\"")
    message(STATUS "mpi c library \"${mpi_c_library}\"")

    list(POP_BACK CMAKE_MESSAGE_INDENT)

    # Set the Tecplot path
    set(tecplot_path ${checkout_src_root}/third_party_open/Tecplot/include)
    message(STATUS "Tecplot set to ${tecplot_path}")

    if(intel_version EQUAL 23)
        set(petsc_path ${checkout_src_root}/third_party_open/petsc/petsc-3.19.4/include)
    else()
        set(petsc_path ${checkout_src_root}/third_party_open/petsc/petsc-3.21.3/include)
    endif()

    #also set PETSC library directory
    set (petsc_libdir ${petsc_path}/../lib/x64/Release-oneAPI/)
    message(STATUS "PETSC")
    list(APPEND CMAKE_MESSAGE_INDENT "  ")
    message(STATUS "path ${petsc_path}")
    message(STATUS "library directory ${petsc_libdir}")
    list(POP_BACK CMAKE_MESSAGE_INDENT)

    # Set the GDAL path
    set(gisinternals_path ${checkout_src_root}/third_party_open/GISInternals/release-1911-x64/lib)
    message(STATUS "gisinternals set to ${gisinternals_path}")

    # Set the SQLITE3 path
    set(sqlite3_path ${checkout_src_root}/third_party_open/sqlite3/sqlite-3.44.0)
    message(STATUS "sqlite3 set to ${sqlite3_path}")

    add_definitions(-DHAVE_MPI=1)

    list(POP_BACK CMAKE_MESSAGE_INDENT)
endif()

if (UNIX)
    #Packages
    find_package(Threads REQUIRED)
    find_package(PkgConfig REQUIRED)

    #MPI
    find_package(MPI REQUIRED)
    add_definitions(-DHAVE_MPI)
    set(mpi_module_path ${mpi_include_path})

    #NetCDF
    pkg_check_modules(NETCDF     REQUIRED netcdf)
    pkg_check_modules(NETCDF_FTN REQUIRED netcdf-fortran)

    # Create two separate targets, because the targets created by pkg_check_modules with IMPORTED_TARGET
    # mix variables for both static and dynamic linking of netcdf, and the C library is not explicitly
    # linked by the Fortran target. Use the same names as on windows.
    add_library(netcdf INTERFACE)
    target_include_directories(netcdf INTERFACE ${NETCDF_INCLUDE_DIRS})
    target_link_libraries(netcdf INTERFACE ${NETCDF_LINK_LIBRARIES})

    add_library(netcdff INTERFACE)
    target_include_directories(netcdff INTERFACE ${NETCDF_FTN_INCLUDE_DIRS})
    target_link_libraries(netcdff INTERFACE ${NETCDF_FTN_LINK_LIBRARIES} netcdf)

    # Petsc
    pkg_check_modules(PETSC REQUIRED PETSc)

    # Proj
    pkg_check_modules(proj REQUIRED proj)
endif(UNIX)

if (UNIX)
    set(platform_extension sh)
    set(library_suffix so)
elseif (WIN32)
    set(platform_extension bat)
    set(library_suffix dll)
endif()

# Expat:
#     Windows: use prebuild library in ".../expat/x64/x64"
#     Linux  : compile source code  using ".../expat/cmake_deltares/CMakeLists.txt"
set(expat_include_path ${checkout_src_root}/third_party_open/expat/x64/include)
if (WIN32)
    set(expat_library_path_win ${checkout_src_root}/third_party_open/expat/x64/x64/$<IF:$<CONFIG:Debug>,Debug,Release>/)
endif(WIN32)

### Configuration names

string(TOUPPER ${CONFIGURATION_TYPE} configuration_type)
string(TOLOWER ${CONFIGURATION_TYPE} configuration_type_lower)

IF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  set(installdir "install_${configuration_type_lower}")
  SET(CMAKE_INSTALL_PREFIX ${checkout_src_root}/../${installdir} CACHE PATH "default" FORCE)
ENDIF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)

if (CMAKE_GENERATOR MATCHES "Visual Studio")
    # Set root name of the projects in the root UI of Visual Studio
    set(visual_studio_source_group_name "Source Files")
    # auto-install debug
    set(CMAKE_VS_INCLUDE_INSTALL_TO_DEFAULT_BUILD 1)

    create_vs_user_files()

endif(CMAKE_GENERATOR MATCHES "Visual Studio")

# Basic (single-component) configurations
set(dflowfm_configuration "DFLOWFM")
set(dflowfm_configuration_interacter "DFLOWFM_INTERACTER")
set(tests_configuration "TESTS")
set(dimr_configuration "DIMR")
set(dwaq_configuration "DWAQ")
set(dwaves_configuration "DWAVES")
set(swan_configuration "SWAN")
set(flow2d3d_configuration "FLOW2D3D")
set(tools_configuration "TOOLS")
set(tools_gpl_configuration "TOOLS_GPL")
set(drr_configuration "DRR")
set(rtc_configuration "RTC")

# Combined (multi-component) configurations for running specific testbenches
# These are equivalent to our branch-prefixes
set(testbench-all_configuration "ALL-TESTBENCH")
set(testbench-fm_configuration "FM-TESTBENCH")
set(testbench-d3d4_configuration "D3D4-TESTBENCH")
set(testbench-rr_configuration "RR-TESTBENCH")
set(testbench-waq_configuration "WAQ-TESTBENCH")
set(testbench-part_configuration "PART-TESTBENCH")
set(testbench-wave_configuration "WAVE-TESTBENCH")
set(testbench-tc_configuration "TC-TESTBENCH")

# Combined (multi-component) configurations
# For products that we release
set(fm-suite_configuration "FM-SUITE")
set(d3d4-suite_configuration "D3D4-SUITE")

# Determine the configuration to make
if(NOT (${configuration_type} STREQUAL ${tests_configuration}
     OR ${configuration_type} STREQUAL ${dflowfm_configuration}
     OR ${configuration_type} STREQUAL ${dflowfm_configuration_interacter}
     OR ${configuration_type} STREQUAL ${dimr_configuration}
     OR ${configuration_type} STREQUAL ${dwaq_configuration}
     OR ${configuration_type} STREQUAL ${dwaves_configuration}
     OR ${configuration_type} STREQUAL ${swan_configuration}
     OR ${configuration_type} STREQUAL ${flow2d3d_configuration}
     OR ${configuration_type} STREQUAL ${tools_configuration}
     OR ${configuration_type} STREQUAL ${tools_gpl_configuration}
     OR ${configuration_type} STREQUAL ${drr_configuration}
     OR ${configuration_type} STREQUAL ${rtc_configuration}
     OR ${configuration_type} STREQUAL ${fm-suite_configuration}
     OR ${configuration_type} STREQUAL ${d3d4-suite_configuration}
     OR ${configuration_type} STREQUAL ${testbench-all_configuration}
     OR ${configuration_type} STREQUAL ${testbench-fm_configuration}
     OR ${configuration_type} STREQUAL ${testbench-d3d4_configuration}
     OR ${configuration_type} STREQUAL ${testbench-rr_configuration}
     OR ${configuration_type} STREQUAL ${testbench-waq_configuration}
     OR ${configuration_type} STREQUAL ${testbench-part_configuration}
     OR ${configuration_type} STREQUAL ${testbench-wave_configuration}
     OR ${configuration_type} STREQUAL ${testbench-tc_configuration}))
   message(FATAL_ERROR "${configuration_type} not recognized. CMake will abort solution generation now.")
endif()

### Component configurations

# Tests configuration
if(${configuration_type} STREQUAL ${tests_configuration})
    message(STATUS "Generating solution for Tests ...")
    include(configurations/components/tests_configuration.cmake)
endif()

# D-Flow FM configuration
if(${configuration_type} STREQUAL ${dflowfm_configuration})
    message(STATUS "Generating solution for D-Flow FM ...")
    include(configurations/components/dflowfm_configuration_without_interacter.cmake)
endif()

# D-Flow FM (with Interacter-GUI) configuration
if(${configuration_type} STREQUAL ${dflowfm_configuration_interacter})
    message(STATUS "Generating solution for D-Flow FM (with interacter) ...")
    include(configurations/components/dflowfm_configuration_interacter.cmake)
endif()

# DIMR configuration
if(${configuration_type} STREQUAL ${dimr_configuration})
    message(STATUS "Generating solution for DIMR ...")
    include(configurations/components/dimr_configuration.cmake)
endif()

# D-WAQ configuration
if(${configuration_type} STREQUAL ${dwaq_configuration})
    message(STATUS "Generating solution for D-Waq ...")
    include(configurations/components/dwaq_configuration.cmake)
    message(STATUS "Finished generating solution for D-Waq")
endif()

# D-Waves configuration
if(${configuration_type} STREQUAL ${dwaves_configuration})
    message(STATUS "Generating solution for D-Waves ...")
    include(configurations/components/dwaves_configuration.cmake)
endif()

# SWAN configuration
if(${configuration_type} STREQUAL ${swan_configuration})
    message(STATUS "Generating solution for SWAN ...")
    include(configurations/components/swan_configuration.cmake)
endif()

# Flow2D3D (Delft3D4) configuration
if(${configuration_type} STREQUAL ${flow2d3d_configuration})
    message(STATUS "Generating solution for Flow2D3D ...")
    include(configurations/components/flow2d3d_configuration.cmake)
endif()

# Tools configuration
if(${configuration_type} STREQUAL ${tools_configuration})
    message(STATUS "Generating solution for Tools ...")
    include(configurations/miscellaneous/tools_configuration_no_fm.cmake)
endif()

# Tools_gpl configuration
if(${configuration_type} STREQUAL ${tools_gpl_configuration})
    message(STATUS "Generating solution for Tools_GPL ...")
    include(configurations/miscellaneous/tools_gpl_configuration.cmake)
endif()

# RTC configuration
if(${configuration_type} STREQUAL ${rtc_configuration})
    message(STATUS "Generating solution for rtc ...")
    include(configurations/components/rtc_configuration.cmake)
endif()

# D-RR configuration
if(${configuration_type} STREQUAL ${drr_configuration})
    message(STATUS "Generating solution for D-RR ...")
    include(configurations/components/drr_configuration.cmake)
endif()

### Composed configurations for testbenches

# ALL configuration
if(${configuration_type} STREQUAL ${testbench-all_configuration})
    message(STATUS "Generating solution for all testbenches ...")
    include(configurations/testbench/all_configuration.cmake)
endif()

# FM configuration
if(${configuration_type} STREQUAL ${testbench-fm_configuration})
    message(STATUS "Generating solution for fm testbench ...")
    include(configurations/testbench/fm_configuration.cmake)
endif()

# D3D4 configuration
if(${configuration_type} STREQUAL ${testbench-d3d4_configuration})
    message(STATUS "Generating solution for d3d4 testbench ...")
    include(configurations/testbench/d3d4_configuration.cmake)
endif()

# RR configuration
if(${configuration_type} STREQUAL ${testbench-rr_configuration})
    message(STATUS "Generating solution for rr testbench ...")
    include(configurations/testbench/rr_configuration.cmake)
endif()

# WAQ configuration
if(${configuration_type} STREQUAL ${testbench-waq_configuration})
    message(STATUS "Generating solution for waq testbench ...")
    include(configurations/testbench/waq_configuration.cmake)
endif()

# PART configuration
if(${configuration_type} STREQUAL ${testbench-part_configuration})
    message(STATUS "Generating solution for part testbench ...")
    include(configurations/testbench/part_configuration.cmake)
endif()

# WAVE configuration
if(${configuration_type} STREQUAL ${testbench-wave_configuration})
    message(STATUS "Generating solution for wave testbench ...")
    include(configurations/testbench/wave_configuration.cmake)
endif()

# TC configuration
if(${configuration_type} STREQUAL ${testbench-tc_configuration})
    message(STATUS "Generating solution for tc testbench ...")
    include(configurations/testbench/tc_configuration.cmake)
endif()

### Composed configurations for releases

# Delft3D-FM suite configuration (dimr, dflowfm, dwaq, dwaves, and some tools)
if(${configuration_type} STREQUAL ${fm-suite_configuration})
    message(STATUS "Generating solution for Delft3D-FM ...")
    include(configurations/suites/fm_configuration.cmake)
endif()

# Delft3D4 suite configuration (dflow2d3d, dwaq, dwaves, and some tools)
if(${configuration_type} STREQUAL ${d3d4-suite_configuration})
    message(STATUS "Generating solution for Delft3D4 ...")
    include(configurations/suites/d3d4_configuration.cmake)
endif()
