subroutine wqint ( agrnam , fd_nefis_waq,&
&laux   ,nsegmt ,segmnt ,nsegtb ,&
&segtab ,npntr  ,nposeg ,pntr   ,&
&nexdef ,exdef  ,nbran  ,branch ,&
&nbrnod ,nnode  ,brnode ,ngrid  ,&
&nqlat  ,qltpar ,waoft  ,afwfqs ,&
&cp     ,qaggr  ,qlaggr ,x      ,&
&tofrom ,segfun ,sexar  ,sexfl  ,&
&slen   ,svol   ,juer   ,ker&
&)

!=======================================================================
!            Rijkswaterstaat/RIZA and DELFT HYDRAULICS
!                One Dimensional Modelling System
!                           S O B E K
!-----------------------------------------------------------------------
! Subsystem:          Water Quality Interface Module
!
! Programmer:         S.L. van der Woude
!
! Module:             WQINT (Water Quality INTerface)
!
! Module description: Convert hydrodynamic results to a water quality
!                     model database. The input file required for DELWAQ
!                     will partly be generated by the User Interface and
!                     partly by the water quality interface program. The
!                     User Interface will insert filenames in the input
!                     file for those items to be generated by the water
!                     quality interface module. DELWAQ will read these
!                     files when the input file is processed.
!
!                     First the water quality interface file is opened
!                     by calling WQOFIL. After that for each time step a
!                     record is read by calling WQRFIL. First a length
!                     table will be calculated by calling WQSLEN.
!                     The time-independent routine WQFRTO is called to
!                     write the from and to lengths of each section.
!                     After this the time depending information will be
!                     written:
!
!                     -   Volumes;
!                     -   Exchange areas;
!                     -   Exchange flows;
!                     -   Waste Loads;
!                     -   Aux files.
!
!                     For this purpose the routine WQFILE is called.
!
!-----------------------------------------------------------------------
! Parameters:
! NR NAME              IO DESCRIPTION
! 24 afwfqs            P  -
!  2 agrnam            P  -
! 16 branch            P  -
! 19 brnode            P  -
! 25 cp                P  -
!  4 datfds            P  -
!  3 deffds            P  -
! 14 exdef             P  -
!  1 filnam            P  -
! 35 juer              P  -
! 36 ker               P  -
!  5 laux              I  TRUE=Auxiliary files must be created.
! 15 nbran             I  Number of branches.
! 17 nbrnod            I  Maximum number of connected branches to one
!                         node.
! 13 nexdef            I  Number of entries in exdef table.
! 20 ngrid             I  Number of grid points in network.
! 18 nnode             I  Number of nodes.
! 10 npntr             I  Number of entries in pntr table.
! 11 nposeg            I  Number of positive segment numbers.
! 21 nqlat             P  -
!  6 nsegmt            I  Number of segments defined.
!  8 nsegtb            I  Number of entries in segtab table.
! 12 pntr              P  -
! 26 qaggr             P  -
! 27 qlaggr            P  -
! 22 qltpar            P  -
! 30 segfun            P  -
!  7 segmnt            P  -
!  9 segtab            P  -
! 31 sexar             P  -
! 32 sexfl             P  -
! 33 slen              P  -
! 34 svol              P  -
! 29 tofrom            P  -
! 23 waoft             P  -
! 28 x                 P  -
!-----------------------------------------------------------------------
! Subprogram calls:
! NAME    DESCRIPTION
! wqaux   Water Quality AUXiliary files
! wqexar  Water Quality EXchange AReas
! wqexfl  Water Quality EXchange FLows
! wqfile  Water Quality output FILE generation
! wqfrto  Water Quality FRom and TO lengths
! wqofil  Water Quality Open FILe
! wqrfil  Water Quality Read FILe
! wqslen  Water Quality Segment LENgths
! wqsvol  Water Quality Segment VOLumes
! wqtime  Water Quality TIME conversion
!=======================================================================
!
!
!
!***********************************************************************
! CVS log information:
!
! $Id$
!
! History:
! $Log: wqint.pf,v $
! Revision 1.4  1999/03/12  12:42:23  kuipe_j
! parallel segments added
!
! Revision 1.3  1998/02/13  12:13:04  kuipe_j
! Adapt to CMT
!
! Revision 1.2  1995/05/30  07:08:39  hoeks_a
! file changed from dos to ux
!
! Revision 1.1  1995/04/13  07:11:01  hoeks_a
! Initial check-in
!
! Revision 1.2  1993/11/26  15:35:44  kuipe_j
! Update after finishing Sobeksel.
!
! Revision 1.1.1.1  1993/07/21  14:44:27  kuipe_j
! Initial version
!
!
!***********************************************************************
!
   include '..\include\sobdim.i'
!
!       Parameters
!
   character*120  agrnam
!
   logical        laux
!
   integer        nbran,&
   &nbrnod,&
   &nexdef,&
   &ngrid,&
   &nnode,&
   &npntr,&
   &nposeg,&
   &nqlat,&
   &nsegmt,&
   &nsegtb,&
   &juer,&
   &ker
!
   integer        branch (4,nbran),&
   &brnode (nbrnod+1,nnode),&
   &pntr   (4,npntr),&
   &segmnt (3,nsegmt),&
   &fd_nefis_waq
!
   real           waoft  (ngrid,dmwaof),&
   &afwfqs (ngrid,6),&
   &cp     (ngrid,4),&
   &segtab (5,nsegtb),&
   &exdef  (6,nexdef),&
   &qaggr  (ngrid,3),&
   &qlaggr (*),&
   &qltpar (9,*),&
   &x      (ngrid),&
   &tofrom (2,npntr),&
   &segfun (nposeg,4),&
   &sexar  (npntr),&
   &sexfl  (npntr),&
   &slen   (nposeg),&
   &svol   (nposeg,3)
!
!     Variables
!
   integer   istep,  nots,  dlwqtm(2), tstart(2), time, dt,&
   &i
   real      psi
!
!     Open hydrodynamic database files and read no of time steps stored
!
   call wqofil(agrnam, fd_nefis_waq, nots, psi, juer, ker)
!
!     Do for each time step
!
   do 100 istep = 1, nots
!
!       Read data from time step "istep"
!
      call wqrfil (fd_nefis_waq,&
      &ngrid      ,nqlat      , istep     ,&
!                     <Af>        <Afs>      <At>
      &waoft(1,3) ,afwfqs(1,1),waoft(1,4) ,&
!                                            <Wt>        <Wfs>
      &cp         ,dlwqtm     ,waoft(1,2) ,afwfqs(1,3),&
      &qaggr      ,qlaggr     ,&
      &juer       ,ker&
      &)
!
!       Calculate the length of each segment (Only first time)
!
      if (istep .eq. 1) then
!
!           Calculate length of each segment
!
         call wqslen ( nsegmt ,segmnt ,&
         &nsegtb ,segtab ,&
         &ngrid  ,nposeg ,&
         &x      ,slen&
         &)
!
!           Calculate the from and to lengths
!
         call wqfrto ( nposeg  ,slen  ,&
         &npntr  ,pntr  , tofrom ,&
         &exdef  ,nexdef&
         &)
!
!           Store current time = starting time
!
         tstart(1) = dlwqtm(1)
         tstart(2) = dlwqtm(2)
         time    = 0
      else
!
!           Calculate current water quality time
!
         call wqtime ( tstart, dlwqtm, dt )
!
!           Save time for new starting point
!
         tstart(1) = dlwqtm(1)
         tstart(2) = dlwqtm(2)

!           copy volumes to 'old' volumes

         do i =1,nposeg
!              <svolo>     <svol>
            svol(i,2) = svol(i,1)
         enddo
!
      endif
!
!       Calculate the segment volumes
!
      call wqsvol ( nsegmt  ,segmnt ,&
      &nsegtb ,segtab ,&
      &ngrid  ,nposeg ,psi    ,&
!                        <At>       <Afs>
      &waoft(1,4),afwfqs(1,1) ,&
      &x      ,svol(1,1)      )
      if (istep .gt. 1) then

         call wqmbps (ngrid , nqlat , nsegmt, nsegtb, nexdef, nposeg,&
         &npntr , dt    , exdef , segmnt, segtab, pntr  ,&
!                                      <qlatgr>        <qunkno>
         &qltpar, qlaggr, waoft(1,18)   , svol(1,3)     ,&
!                      <svol>          <svolo>
         &svol(1,1)     , svol(1,2)     , x     , sexfl )
!
!          Write data of previous step to output files to be used
!          by Delwaq
!
         call wqfile( laux   ,istep-1,nots   ,&
         &time   ,npntr  ,nposeg ,tofrom ,&
!                                              <svolo>
         &segfun ,sexar  ,sexfl  ,svol(1,2) )
!
!          Calculate new time in seconds using delta t
!
         time = time + dt
      endif

!       Calculate the exchange areas
!
      call wqexar ( npntr  ,pntr   ,&
      &nexdef ,exdef  ,&
      &ngrid  ,&
!                        <Af>       <Afs>
      &waoft(1,3),afwfqs(1,1),&
      &sexar&
      &)
!
!       Calculate the exchange flows
!
      call wqexfl ( npntr  ,pntr   ,nexdef ,exdef  ,&
      &ngrid  ,nbran  ,branch ,nbrnod ,&
      &nnode  ,brnode ,qltpar ,x      ,&
      &qaggr  ,qlaggr ,sexfl  )
!
!        Calculate the auxiliary files
!
      if (laux) then
         call wqaux ( nsegmt ,segmnt ,nsegtb ,segtab ,&
         &ngrid  ,nposeg ,psi    ,&
!                          <Af>             <Afs>
         &waoft(1,3)     ,afwfqs(1,1),&
         &cp     ,qaggr  ,&
!                          <Wt>             <Wfs>
         &waoft(1,2)     ,afwfqs(1,3),&
         &x      ,slen   ,&
!                          <Chezy>        <Horizontal>
         &segfun(1,1)    ,segfun(1,2),&
!                          <Velocity>     <Widths>
         &segfun(1,3)    ,segfun(1,4)&
         &)
      endif
!
100 continue
!
!       Write data of last step to output for Delwaq
!
   call wqfile( laux   ,nots   ,nots   ,&
   &time   ,npntr  ,nposeg ,tofrom ,&
!                                           <svol>
   &segfun ,sexar  ,sexfl  ,svol(1,1))
!
   return
end
