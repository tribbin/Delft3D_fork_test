      subroutine CSTABL(ngrid  ,maxlev ,nlev   ,hlev   ,wft    ,aft    ,
     +                  wtt    ,att    ,of     ,lsalt  ,izwft  ,nbran  ,
     +                  branch ,typcr  ,sectc  ,sectv  ,prslot ,psltvr ,
     +                  bfrict ,bfricp ,engpar ,grsize ,
     +                  maxtab ,ntabm  ,ntab   ,table  ,ws     ,lsedt  ,
     +                  juer   ,ker    )

c=======================================================================
c            Rijkswaterstaat/RIZA and DELFT HYDRAULICS
c                One Dimensional Modelling System
c                           S O B E K
c-----------------------------------------------------------------------
c Subsystem:          Cross Sectional Table Module
c
c Programmer:         J.Brouwer
c
c Module:             CSTABL (Cross Sectional TABLes)
c
c Module description: In subroutine CSTABL the following tables will be
c                     generated:
c
c                     - flow areas;
c                     - total areas;
c                     - wetted perimeter
c                     - integrated zWf(z)dz
c
c                     In addition tables related to vertical division of
c                     cross sections will be generated for:
c
c                     - flow area Af(h0) for main section
c                     - flow area Af(h1) for sub section 1
c                     - wetted perimeter O0 for main section
c                     - wetted perimeter O1 for sub section 1
c
c                     The last step of this module concerns the creation
c                     of preissmann slots (funnel) for normal branches. A 
c                     slot will be created if the user selected this option.
c
c                     In subroutine CSTABL tables will be computed for
c                     the flow and total area as well as the wetted
c                     perimeters. Input for this subroutine are the
c                     tables with flow and total widths. These tables
c                     are supplied by the user or are read from a
c                     restart file. CSTABL must be called at the start
c                     of a simulation and each time the morphology mod-
c                     ule adapted the bed levels.
c
c                     The calculation will only be performed for
c                     branches with the cross section type 'TABLE'. For
c                     the other possibilities 'CIRCLE' and 'SEDREDGE'
c                     special routines are used which calculate the
c                     different parameters numerically.
c
c                     The area tables will be generated by means of
c                     numerical integration (CSINTG).
c
c                     The table of wetted perimeters is computed in
c                     subroutine CSPERI from the table of flow widths !
c                     The table with integrated Wf(z) is calculated in
c                     subroutine CSZWFZ.
c
c                     Subroutine CSSECT computes some parameters needed
c                     for the Chezy computation in main sections and sub
c                     sections. In this subroutine the time-independent
c                     parameters:
c
c                     Af(h0) = flow area in the main section for h=h0,
c                     and
c                     Af(h1) = flow area in sub section 1 for h=h1, will
c                     be computed.
c
c                     In addition in subroutine CSSECT the time indepen-
c                     dent wetted perimeters O0 and O1 will be computed,
c                     for main section (h=h0) resp. sub section 1
c                     (h=h1).
c
c-----------------------------------------------------------------------
c Parameters:
c NR NAME              IO DESCRIPTION
c  6 aft               P  -
c  8 att               P  -
c 20 bfricp            P  -
c 19 bfrict            P  -
c 13 branch            P  -
c 21 engpar            P  -
c 22 grsize            P  -
c  4 hlev              P  -
c 11 izwft             P  -
c 27 juer              P  -
c 28 ker               P  -
c 10 lsalt             I  Logical indicator for salt computation
c                         = .true.  : with salt computation
c                         = .false. : no salt computation
c  2 maxlev            I  Maximum+1 over nlev(1:ngrid); needed for
c                         declarations
c 23 maxtab            I  Maximum number of defined tables.
c 12 nbran             I  Number of branches.
c  1 ngrid             I  Number of grid points in network.
c  3 nlev              P  -
c 25 ntab              P  -
c 24 ntabm             I  Maximum size of table (Used for dimensioning
c                         table).
c  9 of                P  -
c 17 prslot(3,nbran)   I  Contains information concerning Preissmann
c                         slot (assuring positive water depths).
c                         (1,i) = Create slot indicator
c                                 csldis (0) : No slot in this branch.
c                                 cslena (1) : Create slot in branch.
c                         (2,i) = Reference area for slot generation.
c                         (3,i) = Depth of slot below lowest bottom of
c                                 branch.
c 18 psltvr            P  -
c 15 sectc             P  -
c 16 sectv             P  -
c 26 table             P  -
c 14 typcr             P  -
c  5 wft               P  -
c  7 wtt               P  -
c-----------------------------------------------------------------------
c Subprogram calls:
c NAME    DESCRIPTION
c csintg  Cross Section INTeGration of tables
c csperi  Cross Sections computation of PERImeter
c cspslt  Cross Section Preissmann SLoT
c cssect  Cross Section SECTional parameters
c cszwfz  Cross Section z Wf(z) dz
c=======================================================================
c
c
c***********************************************************************
c CVS log information:
c
c $Id$
c
c History:
c $Log: cstabl.pf,v $
c Revision 1.8  1999/03/15  15:49:10  kuipe_j
c tabs removed
c
c Revision 1.7  1997/09/30  09:23:01  kuipe_j
c density term improved for Preisman slot
c
c Revision 1.6  1997/06/04  11:18:07  kuipe_j
c Initialize arrays
c
c Revision 1.5  1997/01/23  08:28:48  kuipe_j
c Make flow module robust
c
c Revision 1.4  1996/05/30  09:59:55  kuipe_j
c comment char
c
c Revision 1.3  1995/05/30  09:54:26  hoeks_a
c Minor changes
c
c Revision 1.2  1995/05/30  06:55:32  hoeks_a
c files changed from dos-file to unix-files
c
c Revision 1.1  1995/04/13  06:58:35  hoeks_a
c Initial check-in
c
c Revision 1.2  1993/11/26  15:29:56  kuipe_j
c Update after finishing Sobeksel.
c
c Revision 1.1.1.1  1993/07/21  14:43:41  kuipe_j
c Initial version
c
c
c***********************************************************************
c
c     Include sobek constants
c
      include '../include/sobcon.i'
      include '../include/sobdim.i'
c
c     Declaration of Parameters:
c
      integer ngrid, maxlev, nbran, maxtab, ntabm, juer, ker
c
      integer branch(4,nbran),
     +        typcr(nbran)   ,
     +        nlev(ngrid)    ,
     +        bfrict(3,nbran),
     +        ntab(4,maxtab)
c
      real    wft (ngrid,maxlev) , aft  (ngrid,maxlev),
     +        wtt (ngrid,maxlev) , att  (ngrid,maxlev),
     +        of  (ngrid,maxlev) , izwft(ngrid,maxlev),
     +        sectc(ngrid,3)     , sectv(ngrid,dmsecv),
     +        prslot(3,nbran)    , psltvr(7,ngrid)    ,
     +        bfricp(6,ngrid)    , engpar(9)          ,
     +        grsize(4,ngrid,*)  , table(ntabm)       ,
     +        ws    (ngrid) 
c
      double precision hlev(ngrid,maxlev)
c
      logical lsalt, lsedt
c
c     External function
c
      real      FLBOTT
c
c     Local variables
c
      integer ibr, i
c
c     Computation of Af-table from Wf-table
c     [ Doc. S-FO-001.5KV / Eq. 4-1 ]
c
      call CSINTG (ngrid, maxlev, nlev, hlev, wft, aft)
c
c     Computation of At-table from Wt-table
c     [ Doc. S-FO-001.5KV / Eq. [-] ]
c
      call CSINTG (ngrid, maxlev, nlev, hlev, wtt, att)
c
c     Computation of Of-table from Wf-table
c     [ Doc. S-FO-001.5KV / Eq. [-] ]
c     Of = wetted perimeter
c
      call CSPERI (ngrid, maxlev, nlev, hlev, wft, of)
c
c     Compute flow areas and wetted perimeters for main section
c     and subsection 1.
c
      call CSSECT (ngrid      ,nbran      ,branch     ,typcr      ,
     +             maxlev     ,nlev       ,hlev       ,wft        ,
c                                          <subsec>    <secth0>
     +             aft        ,of         ,sectc(1,1) ,sectv(1,2) ,
c                  <secth1>    <wfh0>      <afh0>      <oh0>
     +             sectv(1,3) ,sectc(1,2) ,sectv(1,4) ,sectv(1,6) ,
c                  <wfh1>      <afh1>      <oh1>
     +             sectc(1,3) ,sectv(1,5) ,sectv(1,7) ,psltvr     ,
c                  <secths>
     +             sectv(1,8) ,ws         ,lsedt      )
c
c     Compute table for integral [z * Wf(z)]
c     See coefficient A1m in d(rho)/dx term of momentum equation
c     [ Doc. S-FO-001.5KV / pg. 5-6 ]
c
      if ( lsalt ) then
         call CSZWFZ (ngrid  ,
     +                maxlev ,nlev   ,hlev   ,
     +                wft    ,izwft  )
      endif
c
c     Create a preissmann slot for branches with slot code = 1
c     Initialize for output purposes
c
      do 90 i=1,ngrid
         psltvr(5,i) = FLBOTT (i    ,ngrid  ,maxlev ,hlev)

  90  continue
c
      do 100 ibr = 1, nbran
         if (int(prslot(1,ibr)) .eq. cslena) then
            call CSPSLT ( ibr    ,ngrid  ,nbran  ,branch ,
     +                    prslot ,psltvr ,
     +                    bfrict ,bfricp ,engpar ,grsize ,
     +                    maxtab ,ntabm  ,ntab   ,table  ,
     +                    maxlev ,nlev   ,hlev   ,
     +                    wft    ,aft    ,wtt    ,att    ,
     +                    of     ,juer   ,ker    )
         endif
 100  continue
c
      end
